dnl Process this file with autoconf to produce a configure script.

dnl $Id$
dnl configure.in
dnl Autoconf script for Cassowary
dnl (C) 1999 Greg J. Badros

dnl ## Initialize autoconf ##
AC_INIT(c++/ClSimplexSolver.cc)
AC_PREREQ(2.4)

dnl ## Initialize automake ##
AM_INIT_AUTOMAKE(cassowary, 0.4)
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(c++/cassowary/config.h)

dnl ## Checks for programs ##
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB
AM_PROG_LEX
AC_PROG_YACC
AM_PROG_LIBTOOL

dnl ## Package options ##
## ACE == AC-Extended -- this was SCWM_ARG_PATH
AC_DEFUN(ACE_ARG_PATH,
[
  AC_ARG_WITH($1,[$2],[
    if test -z "${withval}" || test "yes" = "${withval}" || test "no" = "${withval}"; then
      AC_MSG_ERROR(missing pathname argument to --with-$1)
    elif test ! -d "${withval}"; then
      AC_MSG_ERROR(invalid pathname argument (not a dir, or does not exist) to --with-$1: ${withval})
    else
      $3
    fi],
    $4)
])


AC_ARG_ENABLE(check-integrity,
[  --enable-check-integrity   Verify the integrity of solver data structures at runtime]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_SOLVER_CHECK_INTEGRITY)
  fi
])


AC_ARG_ENABLE(no-deprecated-api,
[  --enable-no-deprecated-api Disallow deprecated API functions to be invoked]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_NO_DEPRECATED)
  fi
])


AC_ARG_ENABLE(trace,
[  --enable-trace             Turn on trace messages to STDERR]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_TRACE)
  fi
])


AC_ARG_ENABLE(hash_map_and_set,
[  --enable-hash-map-and-set  Use hash_map, hash_set for the tableau data structures]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_USE_HASH_MAP_AND_SET)
  fi
])


AC_ARG_ENABLE(no-io,
[  --enable-no-io             Turn off all I/O from the library (for embedded sytems)]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_NO_IO)
  fi
])

AC_ARG_ENABLE(stats-display,
[  --enable-stats-display     Display solver statistics at runtime]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_SOLVER_STATS)
  fi
])

CPPEXTRAFLAGS=""
AC_ARG_ENABLE(warnings,
[  --enable-warnings          Turn on most compiler warnings]
,[if test "yes" = "$enableval"; then
   CPPEXTRAFLAGS="-Wall -W"
  fi
])
AC_SUBST(CPPEXTRAFLAGS)


JAVACFLAGS="-g -O"
AC_ARG_ENABLE(javac-flags,
[  --enable-javac-flags=FLAGS Set the javac compile flags to FLAGS]
,[JAVACFLAGS="$enableval"])
AC_SUBST(JAVACFLAGS)


guile_prefix=${prefix}

ACE_ARG_PATH(guile-prefix,
[  --with-guile-prefix=DIR    Expect guile to be installed in DIR [optional]],
[
	AC_MSG_CHECKING(where to expect to find guile)
	guile_prefix="${withval}"
	
	AC_MSG_RESULT("${guile_prefix}")
])

GUILE_BIN_DIR="${guile_prefix}/bin"
GUILE_INCLUDES="-I${guile_prefix}/include"
GUILE_LIB_DIR="${guile_prefix}/lib"


guile_exec_prefix=${exec_prefix}

ACE_ARG_PATH(guile-exec-prefix,
[  --with-guile-exec-prefix=DIR 
                             Expect guile binaries to be installed in DIR 
                             [optional]],
[
	AC_MSG_CHECKING(where to expect to find guile binaries)
	guile_exec_prefix="${withval}"
	
	AC_MSG_RESULT("${guile_exec_prefix}")
])
GUILE_BIN_DIR="${guile_exec_prefix}"

if test -z "${GUILE_BIN_DIR}"; then
	AC_PATH_PROGS(GUILE_CONFIG, guile-config build-guile, no)
else
	AC_PATH_PROGS(GUILE_CONFIG, guile-config build-guile, no, ${GUILE_BIN_DIR})
fi


if test "no" != "$GUILE_CONFIG"; then
  AC_MSG_CHECKING(whether guile-config or build-guile works)
  if (${GUILE_CONFIG} link) >/dev/null 2>&1; then
    GUILE_CONFIG_works=yes
  else
    GUILE_CONFIG_works=no
  fi
  AC_MSG_RESULT($GUILE_CONFIG_works)
else
  GUILE_CONFIG_works=no
fi

if test -z "${GUILE_BIN_DIR}"; then
	AC_PATH_PROG(GUILE, guile, no)
else
	AC_PATH_PROG(GUILE, guile, no, ${GUILE_BIN_DIR})
fi

AC_MSG_CHECKING(what the guile pkgdatadir is)
GUILE_PKGDATA_DIR=`guile-config info pkgdatadir`
AC_MSG_RESULT($GUILE_PKGDATA_DIR)

AC_SUBST(GUILE)
AC_SUBST(GUILE_INCLUDES)
AC_SUBST(GUILE_BIN_DIR)
AC_SUBST(GUILE_LIB_DIR)
AC_SUBST(GUILE_PKGDATA_DIR)

PYTHON_HEADERS=${prefix}/python1.5

ACE_ARG_PATH(python-headers,
[  --with-python-headers=DIR    Expect Python headers to be installed in DIR [optional]],
[
	AC_MSG_CHECKING(where to find python headers)
	PYTHON_HEADERS="${withval}"
	
	AC_MSG_RESULT("${python_headers}")
])

AC_SUBST(PYTHON_HEADERS)


JAVA_CLASS_PATH="./classes"
ACE_ARG_PATH(java-class-path,
[  --with-java-class-path=DIR 
                             Use DIR as the destination for java .class files
                             [optional]],
[
	AC_MSG_CHECKING(where to install java .class files)
	JAVA_CLASS_PATH=$withval
	AC_MSG_RESULT("$JAVA_CLASS_PATH")
])

AC_SUBST(JAVA_CLASS_PATH)

AC_OUTPUT(
	Makefile \
	c++/Makefile \
	java/Makefile \
	java/demos/Makefile \
	java/cda/Makefile \
	guile/Makefile \
	wrappers/Makefile \
	docs/Makefile \
	)

# Not create config-inline.h for the C++ header files to include
# (needed in case another package uses autoconf and includes the Cassowary
#  header files -- w/o this hack the two config.h files and their defs would
#  conflict and cause at least warnings, and maybe really bad things --03/18/99 gjb)
grep '#define CL_' c++/cassowary/config.h > c++/cassowary/config-inline-check.h || true

# replace config-inline.h only if the new version is different
# (avoid rebuilding everything)
d=c++/cassowary
if test ! -f $d/config-inline.h; then 
   mv $d/config-inline-check.h $d/config-inline.h && echo "creating $d/config-inline.h"
else
   { diff -q $d/config-inline-check.h $d/config-inline.h >/dev/null 2>&1 && echo "$d/config-inline.h is unchanged" } || \
	   { mv -f $d/config-inline-check.h $d/config-inline.h && echo "creating $d/config-inline.h" }
fi
rm -f $d/config-inline-check.h
