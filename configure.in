dnl Process this file with autoconf to produce a configure script.

dnl $Id$
dnl configure.in
dnl Autoconf script for Cassowary
dnl (C) 1999 Greg J. Badros

dnl ## Initialize autoconf ##
AC_INIT(c++/ClSimplexSolver.cc)
AC_PREREQ(2.4)

dnl ## Initialize automake ##
AM_INIT_AUTOMAKE(cassowary, 0.51)
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(c++/config.h)

dnl ## Checks for programs ##
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB
AM_PROG_LEX
AC_PROG_YACC
AM_PROG_LIBTOOL

dnl ## Package options ##
## ACE == AC-Extended -- this was SCWM_ARG_PATH
AC_DEFUN(ACE_ARG_PATH,
[
  AC_ARG_WITH($1,[$2],[
    if test -z "${withval}" || test "yes" = "${withval}" || test "no" = "${withval}"; then
      AC_MSG_ERROR(missing pathname argument to --with-$1)
    elif test ! -d "${withval}"; then
      AC_MSG_ERROR(invalid pathname argument (not a dir, or does not exist) to --with-$1: ${withval})
    else
      $3
    fi],
    $4)
])


AC_ARG_ENABLE(check-integrity,
[  --enable-check-integrity   Verify the integrity of solver data structures at runtime]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_SOLVER_CHECK_INTEGRITY)
  fi
])


TEST_PROGRAMS=""
BENCH_PROGRAMS=""
AC_ARG_ENABLE(cxx-tests,
[  --enable-cxx-tests   Build various C++ test/benchmark programs in addition to the library]
,[if test "yes" = "$enableval"; then
    CL_BUILD_TESTS="true"
    TEST_PROGRAMS='$(test_programs)'
    BENCH_PROGRAMS='${bench_programs}'
  fi
])

AM_CONDITIONAL(CL_BUILD_TESTS, test "true" = "CL_BUILD_TESTS")
AC_SUBST(TEST_PROGRAMS)
AC_SUBST(BENCH_PROGRAMS)


sources_for_fd_solver="ClFDSolver.cc ClFDVariable.cc ClFDConnectorVariable.cc ClFDBinaryOneWayConstraint.cc"

AC_ARG_ENABLE(fd-solver,
[  --enable-fd-solver   Enable the restricted finite domain solver]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_BUILD_FD_SOLVER)
    SOURCES_FOR_FD_SOLVER="$sources_for_fd_solver"
  fi
])
AC_SUBST(SOURCES_FOR_FD_SOLVER)


dnl # Check for the GTL library (needed for fd solver)
dnl GTL_LIB=""
dnl AC_CHECK_LIB(GTL, GML_init, [
dnl GTL_LIB="-lGTL"
dnl AC_DEFINE(CL_HAVE_GTL)
dnl ], no_gtl_lib=yes,)

AC_SUBST(GTL_LIB)


ACE_ARG_PATH(gtl,
[  --with-gtl=DIR   Use GTL installation from DIR
                         can omit DIR to use $prefix ],
[
	AC_DEFINE(CL_HAVE_GTL)
	CL_HAVE_GTL="yes"
	AC_MSG_CHECKING(where to expect to find GTL headers and library)
	if test -z "${withval}" || test "yes" = "${withval}"; then
	   AC_MSG_RESULT("defaulting to the prefix = ${prefix}")
	   withval=${prefix}
	else
	   AC_MSG_RESULT("${withval}")
        fi
	if test -d "${withval}/include/GTL"; then
           GTL_INCLUDES="-I${withval}/include"
        elif test -d "${withval}/share/include/GTL"; then
           GTL_INCLUDES="-I${withval}/share/include"
        fi
	case "$ac_R_nospace" in
	"yes") gtl_lib_path="-L${withval}/lib -R${withval}/lib" ;;
	"no")  gtl_lib_path="-L${withval}/lib -R ${withval}/lib" ;;
	*)     gtl_lib_path="-L${withval}/lib" ;;
	esac
	GTL_LIB="${gtl_lib_path} -lGTL"
])
AC_SUBST(GTL_LIB)
AC_SUBST(GTL_INCLUDES)


AC_ARG_ENABLE(no-deprecated-api,
[  --enable-no-deprecated-api Disallow deprecated API functions to be invoked]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_NO_DEPRECATED)
  fi
])


AC_ARG_ENABLE(trace,
[  --enable-trace             Turn on trace messages to STDERR]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_TRACE)
  fi
])

AC_ARG_ENABLE(cpp_build, [ --disable-cpp-build  Do not build C++ version ],
		enable_cpp_build=no, enable_cpp_build=yes)
if test x"$enable_cpp_build" = x"no" ; then
    HAVE_CPP="false"
else
    HAVE_CPP="true"
fi
AM_CONDITIONAL(HAVE_CPP, test "true" = "$HAVE_CPP")



AC_ARG_ENABLE(hash_map_and_set,
[  --enable-hash-map-and-set  Use hash_map, hash_set for the tableau data structures]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_USE_HASH_MAP_AND_SET)
  fi
])


AC_ARG_ENABLE(no-io,
[  --enable-no-io             Turn off all I/O from the library (for embedded sytems)]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_NO_IO)
  fi
])

AC_ARG_ENABLE(stats-display,
[  --enable-stats-display     Display solver statistics at runtime]
,[if test "yes" = "$enableval"; then
    AC_DEFINE(CL_SOLVER_STATS)
  fi
])

CPPEXTRAFLAGS=""
AC_ARG_ENABLE(warnings,
[  --enable-warnings          Turn on most compiler warnings]
,[if test "yes" = "$enableval"; then
   CPPEXTRAFLAGS="-Wall -W"
  fi
])
AC_SUBST(CPPEXTRAFLAGS)



GUILE_BIN_DIR=""
GUILE_INCLUDES=""

guile_lib_path=""

if test "NONE" = "$exec_prefix"; then
	if test "NONE" = "$prefix"; then
		guile_lib_path="$ac_default_prefix/lib"
	else
		guile_lib_path="$prefix/lib"
	fi
else
        guile_lib_path="$exec_prefix/lib"
fi
if test "NONE" = "$prefix"; then
        GUILE_INCLUDES="-I$ac_default_prefix/include"
else
        GUILE_INCLUDES="-I$prefix/include"
fi

ACE_ARG_PATH(guile-prefix,
[  --with-guile-prefix=DIR Expect guile to be installed in DIR [optional]],
[
	AC_MSG_CHECKING(where to expect to find guile)
	guile_prefix="${withval}"
	GUILE_BIN_DIR="${guile_prefix}/bin"
	GUILE_INCLUDES="-I${guile_prefix}/include"
	guile_lib_path="${guile_prefix}/lib"
	
	AC_MSG_RESULT("${guile_prefix}")
])

ACE_ARG_PATH(guile-exec-prefix,
[  --with-guile-exec-prefix=DIR 
                          Expect guile binaries to be installed in DIR 
                          [optional]],
[
	AC_MSG_CHECKING(where to expect to find guile binaries)
	guile_exec_prefix="${withval}"
	GUILE_BIN_DIR="${guile_exec_prefix}"
	
	AC_MSG_RESULT("${guile_exec_prefix}")
])

if test -z "${GUILE_BIN_DIR}"; then
	AC_PATH_PROGS(GUILE_CONFIG, guile-config build-guile, no)
else
	AC_PATH_PROGS(GUILE_CONFIG, guile-config build-guile, no, ${GUILE_BIN_DIR})
fi


if test "no" != "$GUILE_CONFIG"; then
  AC_MSG_CHECKING(whether guile-config or build-guile works)
  if (${GUILE_CONFIG} link) >/dev/null 2>&1; then
    GUILE_CONFIG_works=yes
  else
    GUILE_CONFIG_works=no
  fi
  AC_MSG_RESULT($GUILE_CONFIG_works)
else
  GUILE_CONFIG_works=no
fi


if test -z "${GUILE_BIN_DIR}"; then
	AC_PATH_PROG(GUILE, guile, no)
else
	AC_PATH_PROG(GUILE, guile, no, ${GUILE_BIN_DIR})
fi

AC_MSG_CHECKING(what the guile pkgdatadir is)
GUILE_PKGDATA_DIR=`guile-config info pkgdatadir`
AC_MSG_RESULT($GUILE_PKGDATA_DIR)
GUILE_PKGDATA_DIR=\${prefix}/${GUILE_PKGDATA_DIR##$prefix}
cassoguiledir=$GUILE_PKGDATA_DIR/site/cassowary

AC_SUBST(cassoguiledir)
AC_SUBST(GUILE)
AC_SUBST(GUILE_INCLUDES)
AC_SUBST(GUILE_BIN_DIR)
AC_SUBST(GUILE_LIB_DIR)
AC_SUBST(GUILE_PKGDATA_DIR)
AM_CONDITIONAL(HAVE_GUILE, test "yes" = "$GUILE_CONFIG_works")


PYTHON_HEADER_DIR=${prefix}/python-1.5

ACE_ARG_PATH(python-headers,
[  --with-python-headers=DIR    Expect Python headers to be installed in DIR [optional]],
[
	AC_MSG_CHECKING(where to find python headers)
	PYTHON_HEADER_DIR="${withval}"
	
	AC_MSG_RESULT("${python_headers}")
])

AC_SUBST(PYTHON_HEADER_DIR)

if test -d ${PYTHON_HEADER_DIR}; then
  HAVE_PYTHON=yes
else
  HAVE_PYTHON=no
fi
AM_CONDITIONAL(HAVE_PYTHON, test "true" = "$HAVE_PYTHON")



JAVA_CLASS_PATH="./classes"
ACE_ARG_PATH(java-class-path,
[  --with-java-class-path=DIR 
                             Use DIR as the destination for java .class files
                             [optional]],
[
	AC_MSG_CHECKING(where to install java .class files)
	JAVA_CLASS_PATH=$withval
	AC_MSG_RESULT("$JAVA_CLASS_PATH")
])

AC_SUBST(JAVA_CLASS_PATH)

if test -d ${JAVA_CLASS_PATH}; then
  HAVE_JAVA=yes
else
  HAVE_JAVA=no
fi

AC_ARG_ENABLE(cflags,
[  --enable-cflags=FLAGS   Set CFLAGS to FLAGS [-g -O2]],
[case "$enableval" in
   yes|no) echo "Please specify a parameter for --enable-cflags"
	   exit;;
 esac
 CFLAGS="$enableval"
])


AC_ARG_ENABLE(cxxflags,
[  --enable-cxxflags=FLAGS   Set CXXFLAGS to FLAGS [-g -O2]],
[case "$enableval" in
   yes|no) echo "Please specify a parameter for --enable-cxxflags"
	   exit;;
 esac
 CXXFLAGS="$enableval"
])

AC_ARG_ENABLE(cppextraflags,
[  --enable-cppextraflags=FLAGS   Set CPPEXTRAFLAGS to FLAGS [-g -O2]],
[case "$enableval" in
   yes|no) echo "Please specify a parameter for --enable-cppextraflags"
	   exit;;
 esac
 CPPEXTRAFLAGS="$enableval"
])

AC_SUBST(CPPEXTRAFLAGS)


JAVACFLAGS="-g -O"
AC_ARG_ENABLE(javac-flags,
[  --enable-javac-flags=FLAGS Set the javac compile flags to FLAGS]
,[JAVACFLAGS="$enableval"])
AC_SUBST(JAVACFLAGS)

HAVA_JAVA="false"
AC_ARG_ENABLE(java_build, [ --enable-java-build  Build java version of Cassowary ],
		enable_java_build=yes, enable_java_build=no)
if ! test x"$enable_java_build" = x"no"; then
    which javac && HAVE_JAVA="true"
fi
AM_CONDITIONAL(HAVE_JAVA, test "true" = "$HAVE_JAVA")


AC_OUTPUT(
	Makefile \
	c++/Makefile \
	java/Makefile \
	java/demos/Makefile \
	java/cda/Makefile \
	guile/Makefile \
	wrappers/Makefile \
	docs/Makefile \
	cassowary.spec \
	GTL.spec \
	)

# Not create config-inline.h for the C++ header files to include
# (needed in case another package uses autoconf and includes the Cassowary
#  header files -- w/o this hack the two config.h files and their defs would
#  conflict and cause at least warnings, and maybe really bad things --03/18/99 gjb)
d="c++"
grep '#define CL_' $d/config.h > $d/config-inline-check.h || true
echo "" >> $d/config-inline-check.h  # add a newline

# replace config-inline.h only if the new version is different
# (avoid rebuilding everything)
if test ! -f $d/config-inline.h; then 
   mv $d/config-inline-check.h $d/config-inline.h && echo "creating $d/config-inline.h"
else
   if diff -q $d/config-inline-check.h $d/config-inline.h >/dev/null 2>&1; then
      echo "$d/config-inline.h is unchanged"
   else
      mv -f $d/config-inline-check.h $d/config-inline.h && echo "creating $d/config-inline.h"
   fi
fi
rm -f $d/config-inline-check.h
